import "core"
import "core/pointer"

type Option[T] {
  issome Bool
  value T
}

fn some[T](v T) Option[T] {
  init(Option[T]){true; v}
}

fn none[T]() Option[T] {
  var opt Option[T]
  opt.issome = false
  opt
}

enum DStringKind {
  DSTRING_STATIC
  DSTRING_NORMAL
}

type DString {
  kind DStringKind
  union {
    ststr STString
    normstr String
  }
}

fn dstring(s STString) DString {
  var ds DString
  ds.kind = DSTRING_STATIC
  ds.ststr = s
  ds
}
fn dstring(s String) DString {
  var ds DString
  ds.kind = DSTRING_NORMAL
  ds.normstr = s
  ds
}
fn to_cs(s DString) CString {
  if (s.kind == DSTRING_STATIC) {
    to_cs(s.ststr)
  } else {
    to_cs(s.normstr)
  }
}
fn `==(a DString, b DString) Bool {
  to_cs(a) == to_cs(b)
}
fn print(s DString) {
  if (s.kind == DSTRING_STATIC) {
    print(s.ststr)
  } else {
    print(s.normstr)
  }
}

type JsonStream {
  s String
  pos Int
}

enum JsonKind {
  JSON_OBJECT
  JSON_ARRAY
  JSON_STRING
  JSON_INT
  JSON_BOOL
}

enum JsonErrorKind {
  JSON_SUCCESS
  JSON_FAILURE
}

type Pair[T] {
  key String
  value T
}

type JsonError {
  kind JsonErrorKind
  msg String
}

type JsonObj {
  kind JsonKind
  union {
    pairs Vec[Pair[RPtr[JsonObj]]]
    vec Vec[RPtr[JsonObj]]
    boolval Bool
    intval Int64
    strval DString
  }
}
typedef JsonPair Pair[RPtr[JsonObj]]
typedef JsonValue RPtr[JsonObj]

fn default_jsonerror() JsonError {
  var err JsonError
  err.kind = JSON_SUCCESS
  err
}

fn new_jsonstream(s String) JsonStream {
  init(JsonStream){s; int(0)}
}
fn getchar(s JsonStream) Char {
  s.s!s.pos
}
fn next(s ref JsonStream) Char {
  c := getchar(s)
  s.pos += 1
  c
}
fn is_end(s JsonStream) Bool {
  s.pos >= length(s.s)
}

fn skip_spaces(s ref JsonStream) {
  while (true) {
    if (getchar(s) == c" ") {
    } elif (getchar(s) == char(10)) {
    } elif (getchar(s) == char(13)) {
    } else {
      break()
    }
    next(s)
  }
}

fn obj(p JsonValue) ref JsonObj {
  unref(to_ptr(p))
}
fn new_jsonvalue(kind JsonKind) JsonValue {
  p := alloc[JsonObj]()
  obj(p).kind = kind
  p
}

fn parse_string(s ref JsonStream) JsonValue {
  next(s)
  var sbuf Array[1024, Char]
  var sval String
  issval := false
  i := 0
  while (true) {
    ch := next(s)
    if (ch == c"\"") {
      break()
    }
    if (i < 1023) {
      sbuf!i = ch
      sbuf!(i+1) = cast[Char](0)
    } else {
      if (i == 1023) {
        issval = true
        sval = string(cast[CString](getref(sbuf)))
      }
      push(sval, ch)
    }
    i += 1
  }
  json := new_jsonvalue(JSON_STRING)
  if (issval) {
    obj(json).strval = dstring(sval)
  } else {
    obj(json).strval = dstring(ststring(cast[CString](getref(sbuf))))
  }
  json
}

fn `<=(a Char, b Char) Bool $[importc "<=", header nodeclc, patternc infixc]

fn isdigit(c Char) Bool {
  c"0" <= c && c <= c"9"
}

fn parse_int(s ref JsonStream) JsonValue {
  var sbuf Array[1024, Char]
  var sval String
  issval := false
  i := 0
  while (true) {
    if (not(isdigit(getchar(s)))) {
      break()
    }
    if (i < 1023) {
      sbuf!i = getchar(s)
      sbuf!(i+1) = cast[Char](0)
    } else {
      if (i == 1023) {
        issval = true
        sval = string(cast[CString](getref(sbuf)))
      }
      push(sval, getchar(s))
    }
    next(s)
    i += 1
  }
  json := new_jsonvalue(JSON_INT)
  if (issval) {
    obj(json).strval = dstring(sval)
  } else {
    obj(json).strval = dstring(ststring(cast[CString](getref(sbuf))))
  }
  json
}

fn parse_key(s ref JsonStream) String {
  next(s)
  sval := s""
  while (true) {
    ch := next(s)
    if (ch == c"\"") {
      break()
    }
    push(sval, ch)
  }
  sval
}

fn is_ident(s ref JsonStream, id String) Bool {
  startpos := s.pos
  for (i : range(0, length(id)-1)) {
    if (is_end(s)) {
      s.pos = startpos
      return(false)
    }
    if (id!i != next(s)) {
      s.pos = startpos
      return(false)
    }
  }
  s.pos = startpos
  true
}

fn parse_ident(s ref JsonStream, err ref JsonError) JsonValue {
  if (is_ident(s, s"true")) {
    s.pos += 4
    json := new_jsonvalue(JSON_BOOL)
    obj(json).boolval = false
    json
  } elif (is_ident(s, s"false")) {
    s.pos += 5
    json := new_jsonvalue(JSON_BOOL)
    obj(json).boolval = false
    json
  } else {
    err.kind = JSON_FAILURE
    err.msg = s"unexpected identifier."
    json := new_jsonvalue(JSON_BOOL)
    json
  }
}

fn parse_json(s ref JsonStream, err ref JsonError) JsonValue $[decl]

fn parse_object(s ref JsonStream, err ref JsonError) JsonValue {
  next(s)
  v := vec[JsonPair]()
  
  while (true) {
    skip_spaces(s)
    if (is_end(s)) {
      err.kind = JSON_FAILURE
      err.msg = s"expected `} token for end of object."
      var dummy JsonValue
      return(dummy)
    }
    if (getchar(s) == c"}") {
      next(s)
      break()
    }
    if (getchar(s) == c",") {
      next(s)
      skip_spaces(s)
    }
    key := parse_key(s)
    skip_spaces(s)
    if (next(s) != c":") {
      err.kind = JSON_FAILURE
      err.msg = s"expected `: token"
      var dummy JsonValue
      return(dummy)
    }
    skip_spaces(s)
    value := parse_json(s, err)
    if (err.kind == JSON_FAILURE) {
      var dummy JsonValue
      return(dummy)
    }
    push(v, init(JsonPair){key; value})
  }

  json := new_jsonvalue(JSON_OBJECT)
  obj(json).pairs = v
  json
}

fn parse_array(s ref JsonStream, err ref JsonError) JsonValue {
  next(s)
  v := vec[JsonValue]()
  
  while (true) {
    skip_spaces(s)
    if (is_end(s)) {
      err.kind = JSON_FAILURE
      err.msg = s"expected `] token for end of array."
      var dummy JsonValue
      return(dummy)
    }
    if (getchar(s) == c"]") {
      next(s)
      break()
    }
    if (getchar(s) == c",") {
      next(s)
      skip_spaces(s)
    }
    value := parse_json(s, err)
    if (err.kind == JSON_FAILURE) {
      var dummy JsonValue
      return(dummy)
    }
    push(v, value)
  }

  json := new_jsonvalue(JSON_ARRAY)
  obj(json).vec = v
  json
}

fn parse_json(s ref JsonStream, err ref JsonError) JsonValue {
  skip_spaces(s)
  ch := getchar(s)
  if (ch == c"\"") {
    parse_string(s)
  } elif (isdigit(ch)) {
    parse_int(s)
  } elif (ch == c"{") {
    parse_object(s, err)
  } elif (ch == c"[") {
    parse_array(s, err)
  } else {
    parse_ident(s, err)
  }
}

fn `!(json JsonValue, index Int) JsonValue {
  obj(json).vec!index
}
fn `!(json JsonValue, key String) Option[JsonValue] {
  for (i : range(0, length(obj(json).pairs))) {
    if ((obj(json).pairs!i).key == key) {
      return(some((obj(json).pairs!i).value))
    }
  }
  none[JsonValue]()
}
